if (LOGIN_IS_APPLICATION.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        if (LOGIN_IS_APPLICATION.toBoolean()) {
            applicationId "com.logic.login"
        }
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = "$javaVersion"
        targetCompatibility = "$javaVersion"
    }

    kotlinOptions {
        jvmTarget = "$javaVersion"
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            if (LOGIN_IS_APPLICATION.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':base')

    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "com.alibaba:arouter-api:$aRouterMainVersion"
    kapt "com.alibaba:arouter-compiler:$aRouterCompilerVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"

    if (LOGIN_IS_APPLICATION.toBoolean()) {
        implementation "me.jessyan:autosize:$autoSizeVersion"
    }
}
