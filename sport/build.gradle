if (SPORT_IS_APPLICATION.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        if (SPORT_IS_APPLICATION.toBoolean()) {
            applicationId "com.logic.sport"
        }
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    if (SPORT_IS_APPLICATION.toBoolean()) {
        signingConfigs {
            debug {
                storeFile file('sport.jks')
                storePassword '12345678'
                keyAlias 'sport'
                keyPassword '12345678'
                v1SigningEnabled true
                v2SigningEnabled true
            }

            release {
                storeFile file('sport.jks')
                storePassword '12345678'
                keyAlias 'sport'
                keyPassword '12345678'
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }

        buildTypes {
            debug {
                minifyEnabled false
                useProguard false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.debug
            }

            release {
                minifyEnabled true
                useProguard true
                shrinkResources true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility = "$javaVersion"
        targetCompatibility = "$javaVersion"
    }

    kotlinOptions {
        jvmTarget = "$javaVersion"
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            if (SPORT_IS_APPLICATION.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':base')
    implementation files('libs/AMap3DMap_6.9.2_AMapSearch_6.9.2_AMapTrack_1.1.0_AMapLocation_4.7.0_20190709.jar')
    // AndroidX
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "com.google.android.material:material:$materialVersion"
    // ARouter
    implementation "com.alibaba:arouter-api:$aRouterMainVersion"
    kapt "com.alibaba:arouter-compiler:$aRouterCompilerVersion"
    // EventBus
    implementation "org.greenrobot:eventbus:$eventBusVersion"
}
