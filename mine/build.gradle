if (MINE_IS_APPLICATION.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        if (MINE_IS_APPLICATION.toBoolean()) {
            applicationId "com.logic.mine"
        }
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility = "$javaVersion"
        targetCompatibility = "$javaVersion"
    }

    kotlinOptions {
        jvmTarget = "$javaVersion"
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            if (MINE_IS_APPLICATION.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':utils')

    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.alibaba:arouter-api:$aRouterMainVersion"
    kapt "com.alibaba:arouter-compiler:$aRouterCompilerVersion"

    // DataBinding RecyclerView
    implementation "me.tatarka.bindingcollectionadapter2:bindingcollectionadapter:$bindingCollectionAdapterVersion"
    implementation "me.tatarka.bindingcollectionadapter2:bindingcollectionadapter-recyclerview:$bindingCollectionAdapterVersion"

    // CircleImageView
    implementation "com.mikhaellopez:circularimageview:$circularImageview"

    if (MINE_IS_APPLICATION.toBoolean()) {
        implementation "me.jessyan:autosize:$autoSizeVersion"
    }
}
